select distinct ?target where {
    ?target rdf:type user:TakingPictures .

    # (1) 초기 검색 결과와 동일한 장소에서 찍은 사진 존재
    # <TAKING_PICTURES_IRI> = "IRI_1 IRI_2 ... IRI_N"
    FILTER EXISTS {
        values ?origin { <TAKING_PICTURES_IRI> } .
        ?origin user:atLocation/^user:atLocation ?target .
    }

    # (2) 초기 검색 결과 이벤트와 동일한 카테고리의 이벤트 존재
    # (3) 초기 검색 결과와 동일한 이벤트만 존재
    # <TAKING_PICTURES_IRI> = "IRI_1 IRI_2 ... IRI_N"
    FILTER EXISTS {
        {
            SELECT DISTINCT ?event ?eventStartTime ?eventEndTime {
                VALUES ?origin { <TAKING_PICTURES_IRI> }
                ?origin time:hasBeginning/time:inXSDDateTimeStamp ?originStartTime .
                ?origin time:hasEnd/time:inXSDDateTimeStamp ?originEndTime .

                ?event a/rdfs:subClassOf user:ScheduledEvent .
                ?event time:inXSDBeginningDateTimeStamp ?eventStartTime .
                ?event time:inXSDEndDateTimeStamp ?eventEndTime .
                FILTER (?originEndTime >= ?eventStartTime && ?eventEndTime >= ?originStartTime)
            }
        }
        ?target time:hasBeginning/time:inXSDDateTimeStamp ?targetStartTime .
        ?target time:hasEnd/time:inXSDDateTimeStamp ?targetEndTime .

        FILTER (?targetEndTime >= ?eventStartTime && ?eventEndTime >= ?targetStartTime)
    }

    # (4) 연관 검색 결과가 초기 검색 결과 외의 사진 보유
    # <TAKING_PICTURES_IRI_COMMA> = "IRI_1, IRI_2, ..., IRI_N"
    FILTER (?target NOT IN (<TAKING_PICTURES_IRI_COMMA>)) .
}
