select distinct ?target where {
    ?target rdf:type user:TakingPictures .

    # (1) 초기 검색 결과와 동일한 여행 기간 내 찍은 사진만 존재
    # <TAKING_PICTURES_IRI> = "IRI_1 IRI_2 ... IRI_N"
    FILTER EXISTS {
        ?targetEventProperty rdfs:subPropertyOf user:event .
        ?travel1 ?targetEventProperty ?target .
        ?travel1 rdf:type user:TravelEngram .

        FILTER EXISTS {
            values ?origin1 { <TAKING_PICTURES_IRI> } .
            ?originProperty rdfs:subPropertyOf user:event .
            ?travel1 ?originProperty ?origin1 .
        }
    }

    # (2) 초기 검색 결과에 포함된 사람과 동일한 사람의 사진 존재
    # <TAKING_PICTURES_IRI> = "IRI_1 IRI_2 ... IRI_N"
    FILTER EXISTS {
        ?target user:withPerson ?person .
        FILTER EXISTS {
            values ?origin2 { <TAKING_PICTURES_IRI> } .
            ?origin2 user:withPerson ?person .
        }
    }

    # (3) 물리적으로 구분되는 다른 장소에서 찍은 사진 존재
    # <TAKING_PICTURES_IRI> = "IRI_1 IRI_2 ... IRI_N"
    FILTER EXISTS {
        ?target user:atLocation ?location3 .
        FILTER NOT EXISTS {
            values ?origin3 { <TAKING_PICTURES_IRI> } .
            ?origin3 user:atLocation ?location3 .
        }
    }
}
